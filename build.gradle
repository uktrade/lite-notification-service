plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.2'
  id "au.com.dius.pact" version "3.5.13"
  id 'jacoco'
}

group 'uk.gov.bis.lite'

project.version = project.hasProperty('projVersion') ? project.projVersion : '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = "uk.gov.bis.lite.notification.NotificationApplication"

ext.dropwizardVersion = '1.2.9'

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "http://dl.bintray.com/gov-uk-notify/maven"
  }
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases"
  }
}

dependencies {
  compile "com.fasterxml.jackson.core:jackson-databind:2.9.7"
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'uk.gov.bis.lite:json-console-appender:1.1.1'
  compile 'uk.gov.bis.lite:basic-auth:1.1'
  compile 'com.amazonaws:aws-java-sdk-sns:1.11.318'
  compile 'uk.gov.service.notify:notifications-java-client:3.9.1-RELEASE'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile 'au.com.dius:pact-jvm-consumer-junit_2.11:3.5.13'

}

shadowJar {
  baseName = 'notification-service'
  classifier = ''
  mergeServiceFiles()
}

build {
  dependsOn shadowJar
}

pact {
  publish {
    pactDirectory = 'target/pacts'
    pactBrokerUrl = 'http://pact-broker.ci.uktrade.io/'
    version = '1.0.0'
  }
}

run { args 'server', 'config.yaml' }

apply plugin: 'maven-publish'

def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/notification/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/notification/api/**/*'
}


publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
  }
  publications {
    api(MavenPublication) {
      artifactId 'lite-notification-service-api'
      version '1.1'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
}
