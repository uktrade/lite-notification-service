plugins {
  id 'application'
  id 'org.flywaydb.flyway' version '4.0.1'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.10"
}

group 'uk.gov.bis.lite'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  project.version = "1.0-SNAPSHOT"
}

sourceCompatibility = 1.8

mainClassName = "uk.gov.bis.lite.notification.NotificationApp"

ext.dropwizardVersion = '1.2.2'

repositories {
  mavenCentral()
  jcenter()
  maven {
    url "http://dl.bintray.com/gov-uk-notify/maven"
  }
  maven {
    name "snapshots"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots"
  }
  maven {
    name "releases"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases"
  }
}


dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'org.quartz-scheduler:quartz:2.1.5'
  compile 'org.postgresql:postgresql:42.1.4'
  compile 'org.flywaydb:flyway-core:4.0.1'
  compile 'net.logstash.logback:logstash-logback-encoder:4.10'
  compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'
  compile 'uk.gov.bis.lite:json-console-appender:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'uk.gov.bis.lite:paas-utils:1.0'

  compile 'uk.gov.service.notify:notifications-java-client:2.0.0-RELEASE'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.3.10'
  testCompile('com.github.tomakehurst:wiremock:2.7.1') {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  testCompile 'org.skyscreamer:jsonassert:1.3.0'

  testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23') {
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'junit', module: 'junit'
  }

  testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.6'
  testCompile 'com.h2database:h2:1.4.196'
}

shadowJar {
  baseName = 'notification-service'
  classifier = ''
  mergeServiceFiles()
}

build {
  dependsOn shadowJar
}

flyway {
  url = 'jdbc:postgresql://localhost:5432/postgres?currentSchema=notificationsvc'
}

jar.enabled = false

run { args 'server', 'config.yaml' }

apply plugin: 'maven-publish'

publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
    maven {
      name "lite-builds"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/lite-builds/"
    }
  }
}

publishing {
  publications {
    service(MavenPublication) {
      artifactId 'notification-service'
      from components.shadow
    }
  }
}
